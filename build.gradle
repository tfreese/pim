// Tasks im SubModul ausführen: gradle MODUL:clean build

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$version_springBoot"
        }

        dependencies {
            dependency "commons-cli:commons-cli:$version_commonsCli"
            dependency "commons-io:commons-io:$version_commonsIo"
            dependency "org.apache.commons:commons-collections4:$version_commonsCollections"
            dependency "org.jsoup:jsoup:$version_jsoup"
        }
    }
}

project("pim-common") {
    apply plugin: "java-library"
    description = "Common Components for PIM"
    dependencies {
        api("commons-cli:commons-cli")
        api("commons-io:commons-io")
        api("javax.activation:javax.activation-api")
        api("org.apache.commons:commons-collections4")
        api("org.apache.commons:commons-lang3")
        api("org.springframework.boot:spring-boot-starter-json")

        testImplementation("org.springframework.boot:spring-boot-starter-test")
    }
}

project("pim-server") {
    apply plugin: "java-library"
    apply plugin: "org.springframework.boot"
    description = "Server for PIM"
    dependencies {
        implementation(project(":pim-common"))

        api("org.springframework.boot:spring-boot-starter-web")
        implementation("de.freese.spring:spring-autoconfigure-hsqldbserver:$project.version")

        implementation("org.flywaydb:flyway-core")
        implementation("org.hsqldb:hsqldb")
        implementation("org.springframework.boot:spring-boot-starter-jdbc")
        implementation("org.springframework.boot:spring-boot-starter-mail") {
            exclude group: "javax.activation", module: "activation"
        }

        runtimeOnly("org.springframework.boot:spring-boot-starter-actuator")
        runtimeOnly("org.xerial:sqlite-jdbc")

        testImplementation("org.junit.jupiter:junit-jupiter-engine")
        testImplementation("org.junit.platform:junit-platform-runner")
        testImplementation("org.springframework.boot:spring-boot-starter-test")
        testImplementation("org.jsoup:jsoup")
    }
    // Start: gradle bootRun --args="--spring.profiles.active=dev"
	// The archive name. If the name has not been explicitly set, the pattern for the name is:
	// [archiveBaseName]-[archiveAppendix]-[archiveVersion]-[archiveClassifier].[archiveExtension]
	// archiveFileName = "my-boot.jar"
    bootJar {
        mainClass = "de.freese.pim.server.PIMServerApplication"
    }
    // gradle bootRun --args="--spring.profiles.active=Server,HsqldbEmbeddedServer --server.port=65111"
    bootRun {
        args = [
        	  "--spring.profiles.active=Server,HsqldbEmbeddedServer"
        	, "--server.port=65111"
        ]
	}
	    
    //Für Platzhalter in application.properties
    ext.artifactId = project.name
    
    processResources {
        filesMatching("application-Server.properties") {
        	filteringCharset = "UTF-8"
        	
        	// Bei Problemen müssen die Platzhalter escaped werden: \${...}
            expand(project.properties)
        }
    }
}

project("pim-clientfx") {
    apply plugin: "java-library"
    apply plugin: "org.springframework.boot"
    apply plugin: "org.openjfx.javafxplugin"
    description = "GUI for PIM"
    test.enabled = false
    dependencies {
        implementation(project(":pim-common"))
        implementation(project(":pim-server"))

        javafx {
        	version = "$version_javafxBase"
            modules = ["javafx.controls", "javafx.fxml", "javafx.web"]
        }
    }
    // Start: gradle bootRun --args="--spring.profiles.active=dev"
	// The archive name. If the name has not been explicitly set, the pattern for the name is:
	// [archiveBaseName]-[archiveAppendix]-[archiveVersion]-[archiveClassifier].[archiveExtension]
	// archiveFileName = "test2-boot.jar"      
    bootJar {
        mainClass = "de.freese.pim.gui.PIMLauncher"
    }    
    // gradle bootRun --args="--spring.profiles.active=ClientStandalone,HsqldbEmbeddedServer"
    bootRun {
        args = [
        	  "--spring.profiles.active=ClientStandalone,HsqldbEmbeddedServer"
        ]
	} 
	
    //Für Platzhalter in application.properties
    ext.artifactId = project.name

    processResources {
        filesMatching("application-ClientEmbeddedServer.properties") {
        		filteringCharset = "UTF-8"
        		
        		// Bei Problemen müssen die Platzhalter escaped werden: \${...}
                expand(project.properties)
            }
    }
}
